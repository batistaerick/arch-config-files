import QtQuick
import QtQuick.Controls.Basic
import "PanelStyle.js" as PanelStyle
import "NotificationLogic.js" as NotificationLogic

Rectangle {
    id: card
    required property var notification
    required property color foreground
    required property color accent
    property bool compact: false
    property bool filled: true
    readonly property bool imageIsPreview: NotificationLogic.imageIsPreview(notification,
        imageLoader.implicitWidth, imageLoader.implicitHeight)
    readonly property bool useNotificationImage: notification && !imageIsPreview
    Image {
        id: imageLoader
        visible: false
        source: card.notification ? card.notification.image : ""
        asynchronous: true
    }
    signal activated(int notificationId)
    function invokeAction(action) {
        var id = notification.id;
        activated(id);
        action.invoke();
    }
    implicitHeight: content.implicitHeight + 28
    radius: PanelStyle.cornerRadius
    color: filled ? Qt.alpha(foreground, 0.055) : "transparent"
    border.width: filled && notification && notification.urgency === 2 ? 1 : 0
    border.color: accent

    // Behind the content so dismiss, action, and reply controls keep their own clicks.
    MouseArea {
        objectName: "activateNotification"
        anchors.fill: parent
        readonly property var defaultAction: card.notification
            ? card.notification.actions.find(action => action.identifier === "default") : null
        enabled: !!defaultAction
        cursorShape: Qt.PointingHandCursor
        onClicked: card.invokeAction(defaultAction)
    }

    Column {
        id: content
        x: 14; y: 14
        width: parent.width - 28
        spacing: 10
        Item {
            id: cardHeader
            width: parent.width
            height: Math.max(30, titleBox.implicitHeight)
            Item {
                id: applicationIcon
                objectName: "notificationAppIcon"
                anchors.verticalCenter: parent.verticalCenter
                width: 28; height: 28
                Image {
                    id: notificationImage
                    objectName: "notificationImage"
                    anchors.fill: parent
                    source: card.useNotificationImage ? card.notification.image : ""
                    sourceSize.width: 56; sourceSize.height: 56
                    fillMode: Image.PreserveAspectFit
                    asynchronous: true
                    visible: status === Image.Ready
                }
                Image {
                    id: fallbackIcon
                    objectName: "notificationFallbackIcon"
                    anchors.fill: parent
                    source: card.notification && card.notification.appIcon ? (card.notification.appIcon.indexOf("/") !== -1 || card.notification.appIcon.indexOf(":") !== -1 ? card.notification.appIcon : "image://icon/" + card.notification.appIcon) : ""
                    fillMode: Image.PreserveAspectFit
                    visible: notificationImage.status !== Image.Ready && status === Image.Ready
                }
                Text {
                    objectName: "notificationFallbackGlyph"
                    anchors.centerIn: parent
                    visible: notificationImage.status !== Image.Ready && fallbackIcon.status !== Image.Ready
                    text: "󰂚"; color: card.foreground
                    font.family: PanelStyle.fontFamily; font.pixelSize: 22
                }
            }
            Column {
                id: titleBox
                x: 38
                anchors.verticalCenter: parent.verticalCenter
                width: parent.width - 28 - 30 - 20
                spacing: 3
                Text {
                    objectName: "notificationTitle"
                    width: parent.width
                    text: card.notification ? card.notification.summary : ""
                    textFormat: Text.PlainText
                    color: card.foreground
                    font.family: PanelStyle.fontFamily; font.pixelSize: PanelStyle.bodySize
                    font.bold: true
                    wrapMode: Text.Wrap
                    maximumLineCount: card.compact ? 2 : 4
                    elide: Text.ElideRight
                }
            }
            PanelButton {
                objectName: "dismissNotification"
                anchors.right: parent.right
                anchors.verticalCenter: parent.verticalCenter
                width: 30; height: 30
                text: "󰅖"; icon: true; compactIconBackground: true
                foreground: card.foreground
                onClicked: if (card.notification) card.notification.dismiss()
            }
        }
        Text {
            visible: text !== ""
            width: parent.width
            text: card.notification ? card.notification.body : ""
            textFormat: Text.PlainText
            color: card.foreground; opacity: 0.85
            font.family: PanelStyle.fontFamily; font.pixelSize: PanelStyle.bodySize
            wrapMode: Text.Wrap
            maximumLineCount: card.compact ? 3 : 12
            elide: Text.ElideRight
        }
        Image {
            objectName: "notificationPreview"
            source: card.imageIsPreview ? imageLoader.source : ""
            visible: status === Image.Ready && card.imageIsPreview
            readonly property real displayScale: Math.min(1,
                content.width / Math.max(1, implicitWidth),
                (card.compact ? 128 : 160) / Math.max(1, implicitWidth),
                (card.compact ? 96 : 120) / Math.max(1, implicitHeight))
            width: visible ? implicitWidth * displayScale : 0
            height: visible ? implicitHeight * displayScale : 0
            fillMode: Image.PreserveAspectFit
            asynchronous: true
        }
        Flow {
            width: parent.width
            spacing: 6
            Repeater {
                model: card.notification ? card.notification.actions.filter(a => a.identifier !== "default" && a.identifier !== "inline-reply") : []
                PanelButton {
                    required property var modelData
                    objectName: "notificationAction_" + modelData.identifier
                    text: modelData.text
                    width: Math.min(content.width, Math.max(90, text.length * 9 + 24))
                    height: 32
                    foreground: card.foreground
                    onClicked: card.invokeAction(modelData)
                }
            }
        }
        Row {
            visible: card.notification && card.notification.hasInlineReply
            width: parent.width; spacing: 8
            TextField {
                id: reply
                objectName: "notificationReply"
                width: parent.width - 76; height: 34
                placeholderText: card.notification ? card.notification.inlineReplyPlaceholder || "Reply…" : ""
                color: card.foreground
                font.family: PanelStyle.fontFamily; font.pixelSize: PanelStyle.bodySize
                background: Rectangle { color: Qt.alpha(card.foreground, 0.08); radius: PanelStyle.controlRadius }
                onAccepted: if (text.trim()) { card.notification.sendInlineReply(text); text = ""; }
            }
            PanelButton {
                text: "Send"; height: 34; foreground: card.foreground
                available: reply.text.trim().length > 0
                onClicked: { card.notification.sendInlineReply(reply.text); reply.text = ""; }
            }
        }
    }
}
